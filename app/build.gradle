apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext.versionMajor = 0
ext.versionMinor = 10
ext.versionPatch = 2
ext.versionClassifier = null
ext.isDev = false
ext.minimumSdkVersion = 23

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "de.rhab.wlbtimer"
        targetSdkVersion 28

        minSdkVersion project.ext.minimumSdkVersion
        versionCode generateVersionCode() // 230010203
        versionName generateVersionName() // 1.2.3-DEV

        resConfigs "en"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// https://medium.com/@maxirosson/versioning-android-apps-d6ec171cfd82
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (!ext.versionClassifier && ext.isDev) {
        ext.versionClassifier = "DEV"
    }
    if (ext.versionClassifier) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

/*
https://github.com/material-components/material-components-android/blob/master/docs/getting-started.md
If you don't want to switch over to the new androidx and com.google.android.material packages yet,
you can use Material Components via the com.android.support:design:28.0.0 dependency.
Note: You should not use the com.android.support and com.google.android.material dependencies in
your app at the same time.
 */

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:customtabs:28.0.0"
    implementation "com.android.support:design:28.0.0"
    implementation "com.android.support:recyclerview-v7:28.0.0"
    implementation "com.android.support:support-compat:28.0.0"
    implementation "com.firebaseui:firebase-ui-auth:4.3.1"
    implementation "com.firebaseui:firebase-ui-database:4.3.1"
    implementation 'com.firebaseui:firebase-ui-firestore:4.3.1'
    implementation "com.google.android.gms:play-services-auth:16.0.1"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "com.google.firebase:firebase-auth:16.1.0"
    implementation "com.google.firebase:firebase-core:16.0.7"
    implementation 'com.google.firebase:firebase-firestore:18.0.0'

    implementation "com.crashlytics.sdk.android:crashlytics:2.9.8"
    implementation "com.github.crysxd:shared-firebase-preferences:1.0.1"
    implementation "com.jakewharton.threetenabp:threetenabp:1.1.1"
    implementation "com.larswerkman:HoloColorPicker:1.5"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android-extensions'
repositories {
    mavenCentral()
}
