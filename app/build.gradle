apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext.versionMajor = 0
ext.versionMinor = 23
ext.versionPatch = 1
ext.versionClassifier = null
ext.isDev = false
ext.minimumSdkVersion = 23

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "de.rhab.wlbtimer"
        targetSdkVersion 30

        minSdkVersion project.ext.minimumSdkVersion
        versionCode generateVersionCode() // 230010203
        versionName generateVersionName() // 1.2.3-DEV

        resConfigs "en"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            //minifyEnabled false
            minifyEnabled true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '29.0.2'
}

// https://medium.com/@maxirosson/versioning-android-apps-d6ec171cfd82
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (!ext.versionClassifier && ext.isDev) {
        ext.versionClassifier = "DEV"
    }
    if (ext.versionClassifier) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

/*
https://github.com/material-components/material-components-android/blob/master/docs/getting-started.md
If you don't want to switch over to the new androidx and com.google.android.material packages yet,
you can use Material Components via the com.android.support:design:28.0.0 dependency.
Note: You should not use the com.android.support and com.google.android.material dependencies in
your app at the same time.
 */

/* 2019-02-14 I tried to switch to androidx but FirestoreRecyclerView is a problem.. so I reverted */

dependencies {
    // Import the platform
    implementation platform('com.google.firebase:firebase-bom:25.7.0')

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation "com.firebaseui:firebase-ui-auth:4.3.1"
    implementation "com.firebaseui:firebase-ui-database:4.3.1"
    implementation 'com.firebaseui:firebase-ui-firestore:4.3.1'
    implementation "com.google.android.gms:play-services-auth:19.0.0"

    // When using a BoM, dependencies don't require a specified version
    // If you do specify a version, it overrides the library version specified in the BoM
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-firestore'

    // replacing this would require Java8 -> API Level 26 -> Android 8.0 (Oreo)
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'

    // seems ok.. used for Category colors
    implementation "com.larswerkman:HoloColorPicker:1.5"

    // bridge to TypeScript - only enable when needed to generate *.d.ts file
//    implementation "com.github.ntrrgc:ts-generator:1.1.1"

    testImplementation 'junit:junit:4.13'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android-extensions'
repositories {
    mavenCentral()
}
